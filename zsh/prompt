setopt prompt_subst

# define colors
autoload colors zsh/terminfo
if [[ "$terminfo[colors]" -ge 8 ]]; then
    colors
fi
for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE; do
    eval PR_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
    eval PR_LIGHT_$color='%{$fg[${(L)color}]%}'
    eval PR_BG_$color='%{$bg[${(L)color}]%}'
done
PR_NO_COLOUR="%{$terminfo[sgr0]%}"

RPROMPT=
function __prompt_update_from_keymap {
  case $1 in
    vicmd)
      RPROMPT='$PR_BG_RED${PR_YELLOW}VI-CMD MODE$PR_NO_COLOUR'
      ;;
    *)
      RPROMPT=
      ;;
  esac
}

# define the prompt
PROMPT='\
$PR_LIGHT_CYAN%(!.%S%n%s.%n)@%m$PR_GREEN:$PR_YELLOW%~
%(?..$PR_RED(%?%))$PR_LIGHT_CYAN%#$PR_NO_COLOUR '

PS2='$PR_BLUE%_$PR_WHITE>$PR_NO_COLOUR '

# keymap reset
function _precmd_prompt_update { __prompt_update_from_keymap main }
precmd_functions+=_precmd_prompt_update

# xterm title
function title {
  if [[ $TERM == "xterm" ]]; then
    print -Pn "\e]0;$*\a"
  fi
}

function _precmd_update_xterm_title {
  title "%n@%m:%~"
}
precmd_functions+=_precmd_update_xterm_title

function _preexec_update_xterm_title {
  emulate -L zsh
  local -a cmd; cmd=(${(z)1})
  title $cmd[1]:t "$cmd[2,-1]"
}
preexec_functions+=_preexec_update_xterm_title

function zle-keymap-select {
  __prompt_update_from_keymap $KEYMAP
  zle reset-prompt
}
zle -N zle-keymap-select

# vim:ft=zsh
