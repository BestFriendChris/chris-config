#compdef znamedir

_znamedir() {
  _arguments -C -s \
    '-v[list current bindings]' \
    '*::znamedir command:_znamedir_command'
}

(( $+functions[_znamedir_command] )) ||
_znamedir_command() {
  local cmd ret=1
  (( $+_znamedir_cmds )) || _znamedir_cmds=(
    'help:help files'
    'list:list current bindings'
    'refresh:refresh the bindings from disk'
    'add:add a new binding to a directory (default is \$PWD)'
    'delete:delete an existing binding'
  )
  if (( CURRENT == 1 )); then
    _describe -t commands 'znamedir command' _znamedir_cmds
  else
    local curcontext="$curcontext"
    cmd="${_znamedir_cmds[(r)$words[1]:*]%%:*}"
    if (( $#cmd )); then
      curcontext="${curcontext%:*:*}:znamedir-$cmd}:"
      _call_function ret _znamedir_$cmd
    else
      _message -r "unknown znamedir command: $words[1]"
    fi
    return ret
  fi
}

(( $+functions[_znamedir_list] )) ||
_znamedir_list() {
  if (( CURRENT == 2 )); then
    local -a _znamedir_names
    for k in ${(k)_namedir_mappings}; do
      _znamedir_names+=("$k:$_namedir_mappings[$k]")
    done
    _describe -t commands 'znamedir bindings' _znamedir_names
  fi
}

(( $+functions[_znamedir_add] )) ||
_znamedir_add() {
  if (( CURRENT == 2 )); then
    _message -r 'new binding name'
  elif (( CURRENT == 3 )); then
    _path_files -/
  fi
}

(( $+functions[_znamedir_delete] )) ||
_znamedir_delete() {
  if (( CURRENT == 2 )); then
    local -a _znamedir_names
    for k in ${(k)_namedir_mappings}; do
      _znamedir_names+=("$k:$_namedir_mappings[$k]")
    done
    _describe -t commands 'znamedir bindings' _znamedir_names
  fi
}

_znamedir "$@"
