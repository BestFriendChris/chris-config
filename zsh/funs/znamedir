#!/bin/zsh

NAMEDIR_FILE=${NAMEDIR_FILE:-$ZDOTDIR/.znamedirs}

typeset -gA _namedir_mappings
local -H _name=$0

function _namedir_usage {
  echo "Usage of $_name:"
  echo "       help               -- this usage doc"
  echo "       list               -- list current bindings"
  echo "  [-v] refresh            -- refresh the bindings from disk"
  echo "  [-v] [-d dir] add name  -- add a new binding to a directory (default is \$PWD)"
  echo "  [-v] delete name        -- delete an existing binding"
}

function _namedir_list {
  if [[ -r $NAMEDIR_FILE ]]; then
    echo "Current namedir bindings:"
    cat $NAMEDIR_FILE | sed "s/^/    /"
  else
    echo "No current namedir bindings"
  fi
}

function _namedir_read {
  # Clean out the namedir mappings
  _namedir_mappings=()
  if [[ -f $NAMEDIR_FILE ]]; then
    local -H IFS='='
    while read key val; do
      _namedir_mappings[$key]=$val
    done < $NAMEDIR_FILE

    if [[ -z $_namedir_mappings ]]; then
      rm $NAMEDIR_FILE
    fi
  fi
}

function _namedir_refresh {
  local -HA _namedir_original_mappings
  _namedir_original_mappings=( ${(kv)_namedir_mappings} )

  _namedir_read

  for name in ${(k)_namedir_original_mappings}; do
    if [[ -z $_namedir_mappings[$name] ]]; then
      unset $name
    fi
  done

  for name in ${(k)_namedir_mappings}; do
    local -H dir=$_namedir_mappings[$name]
    eval "$name=$dir"
    : ~$name
  done
}

function _namedir_write {
  for key in ${(k)_namedir_mappings}; do
    print "$key=$_namedir_mappings[$key]"
  done >| $NAMEDIR_FILE

  _namedir_refresh
}

function _namedir_add {
  _namedir_read
  _namedir_mappings[$1]="\"$2\""
  _namedir_write
}

function _namedir_remove {
  _namedir_read
  unset "_namedir_mappings[$1]"
  _namedir_write

  unset $1
}

local -H _verbose _dir
zparseopts -E -D -a _verbose v -verbose
zparseopts -E -D -a _dir d: -dir:

case $1 in
  (list)
    (( $# == 1 )) || ( _namedir_usage; return 1 )
    _namedir_list
    return 0
    ;;
  (add)
    (( $# == 2 )) || ( _namedir_usage; return 1 )
    if [[ -n $_dir && ! -d $_dir[2] ]]; then
      echo "Directory '$_dir[2]' not found"; return 1
    fi
    local -H _dir_name
    _dir_name=$(
      [[ -n $_dir ]] && cd $_dir[2]
      echo $PWD
    )
    _namedir_add $2 $_dir_name
    ;;
  (delete)
    (( $# == 2 )) || ( _namedir_usage; return 1 )
    _namedir_remove $2
    ;;
  (refresh)
    (( $# == 1 )) || ( _namedir_usage; return 1 )
    _namedir_refresh
    ;;
  (help)
    (( $# == 1 )) || ( _namedir_usage; return 1 )
    _namedir_usage; return 0
    ;;
  (*)
    echo "Unknown args: $*"
    _namedir_usage; return 1
    ;;
esac

[[ -n $_verbose ]] && _namedir_list

return 0
